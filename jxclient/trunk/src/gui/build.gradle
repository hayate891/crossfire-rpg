defaultTasks 'installApp'

evaluationDependsOn(':src:network')

apply plugin: 'application'
apply plugin: 'java'

sourceCompatibility = 1.6

applicationName = 'jxclient'
mainClassName = 'com.realtime.crossfire.jxclient.main.JXClient'
run.args '-ea'

jar.baseName = 'jxclient'
jar.classifier = 'plain'

dependencies {
    compile name: 'annotations', version: '0.0'
    compile name: 'junit', version: '4.2'
    compile project(':src:model')
    compile project(':src:network')
    compile project(':src:protocol')
    compile project(':src:resources')
    compile project(':src:skins')
    compile project(':src:util')
    testCompile project(':src:network').sourceSets.test.output
}

repositories {
    flatDir {
        dirs rootProject.file('lib')
    }
}

task createJar(type: Jar, dependsOn: assemble) {
    description = 'Creates the jxclient.jar application.'
    baseName = 'jxclient'
    manifest.attributes 'Main-Class': mainClassName
    manifest.attributes 'SplashScreen-Image': 'resource/splashscreen.png'
    from sourceSets.main.output
    doFirst {
        // The following must be executed in doFirst since the configuration
        // has not been initialized at configuration time.
        from(configurations.runtime.resolve().collect {
            it.isDirectory() ? it : zipTree(it)
        }) {
            exclude 'META-INF/**'
            exclude 'org/jetbrains/**'
        }
    }
}
