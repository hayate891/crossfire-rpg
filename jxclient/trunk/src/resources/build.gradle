import org.gradle.process.internal.ExecException

defaultTasks 'jar', 'check'

apply plugin: 'java'

ext.buildNumber = {
    String buildNumber
    try {
        ByteArrayOutputStream execStandardOutput = new ByteArrayOutputStream()
        exec {
            commandLine 'svnversion'
            workingDir rootProject.projectDir
            standardOutput = execStandardOutput
        }
        buildNumber = execStandardOutput.toString().replaceAll('^[0-9]+:', '').replaceAll('[^0-9]', '')
        if (buildNumber.isEmpty()) {
            buildNumber = 'unknown'
        }
    } catch (ExecException ex) {
        buildNumber = 'unknown'
    }
    buildNumber
}.memoize()

configure(tasks[sourceSets.main.processResourcesTaskName]) {
    inputs.property 'build.number', { buildNumber() }
    filter { line ->
        if (line.equals('build.number=unknown')) {
            'build.number=' + buildNumber()
        } else {
            line
        }
    }
}
